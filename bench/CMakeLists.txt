cmake_minimum_required(VERSION 3.16.3)
project(rangefilters-bench)

option(ALL_RENCODER "compile all REncoder implementation (SE/SS)" ON)

# fetch latest argparse
include(FetchContent)
FetchContent_Declare(
    argparse
    GIT_REPOSITORY https://github.com/p-ranav/argparse.git
    GIT_TAG        v2.7
)
FetchContent_MakeAvailable(argparse)

set(CMAKE_CXX_STANDARD 17)

list(APPEND SUCCINCT_LIBS "sux" "sdsl-lite")
list(APPEND Targets "memento" "grafite" "surf" "rosetta" "proteus" "snarf" "rencoder" "bucketing")
list(APPEND x86Targets "surf" "rosetta" "proteus" "rencoder")

function(compile_bench ds)
    if (NOT ds IN_LIST Targets)
        message(WARNING "${ds} is not defined")
        return()
    endif()
    if (ds IN_LIST x86Targets AND NOT CMAKE_SYSTEM_PROCESSOR MATCHES "(x86)|(X86)|(amd64)|(AMD64)")
        message(WARNING "cannot compile ${ds} on this architecture")
        return()
    endif()
    add_executable(bench_${ds} filters_benchmark/bench_${ds}.cpp)
    target_link_libraries(bench_${ds} argparse)

    if (ds MATCHES "memento")
        target_link_libraries(bench_memento mementolib)
    elseif (ds MATCHES "grafite")
        add_library(grafitelib INTERFACE)

        if ("sux" IN_LIST SUCCINCT_LIBS)
            message(STATUS "Using sux")
            target_compile_definitions(grafitelib INTERFACE -DSUCCINCT_LIB_SUX)
            target_include_directories(grafitelib INTERFACE ./include/grafite/include ./include/grafite/lib/sux)
        endif()
        if ("sdsl-lite" IN_LIST SUCCINCT_LIBS)
            message(STATUS "Using sdsl-lite")
            target_compile_definitions(grafitelib INTERFACE -DSUCCINCT_LIB_SDSL)
            target_include_directories(grafitelib INTERFACE ./include/grafite/include ./include/grafite/lib/sdsl-lite/include)
        endif()

        set(Boost_USE_STATIC_LIBS OFF)
        set(Boost_USE_STATIC_RUNTIME OFF)
        if (USE_BOOST AND USE_MULTI_THREADED)
            set(Boost_USE_MULTITHREADED ON)
            find_package(Boost REQUIRED COMPONENTS thread)
            message(STATUS "Boost found, using it for the multi-threaded version of the library")
            target_compile_definitions(grafitelib INTERFACE -DUSE_LIBRARY_BOOST_PARALLEL)
            target_link_libraries(grafitelib INTERFACE Boost::thread)
            target_include_directories(grafitelib INTERFACE ${Boost_INCLUDE_DIRS})
        elseif (USE_BOOST)
            set(Boost_USE_MULTITHREADED OFF)
            find_package(Boost REQUIRED)
            message(STATUS "Boost found, using it for the single-threaded version of the library")
            target_compile_definitions(grafitelib INTERFACE -DUSE_LIBRARY_BOOST)
            target_include_directories(grafitelib INTERFACE ${Boost_INCLUDE_DIRS})
        elseif (USE_MULTI_THREADED)
            message(STATUS "Using TBB and std::execution for the multi-threaded version of the library")
            find_package(TBB REQUIRED)
            target_compile_definitions(grafitelib INTERFACE -DUSE_LIBRARY_STL_PARALLEL)
            target_link_libraries(grafitelib INTERFACE tbb)
        else ()
            # target_compile_definitions(grafitelib INTERFACE -DUSE_LIBRARY_STL)
        endif ()

        target_link_libraries(bench_grafite grafitelib)
    elseif (ds MATCHES "bucketing")
        target_link_libraries(bench_bucketing grafitelib)
    elseif (ds MATCHES "rosetta")
        add_library(rosetta STATIC ./include/Rosetta/dst.cpp ./include/Rosetta/MurmurHash3.cpp)
        target_link_libraries(bench_rosetta rosetta)
    elseif(ds MATCHES "proteus")
        add_library(proteus STATIC ./include/Proteus/include/config.cpp ./include/Proteus/include/clhash.cpp ./include/Proteus/include/prefixbf.cpp ./include/Proteus/include/MurmurHash3.cpp ./include/Proteus/include/util.cpp ./include/Proteus/include/modeling.cpp)
        target_link_libraries(bench_proteus proteus)
    elseif(ds MATCHES "snarf" AND NOT CMAKE_SYSTEM_PROCESSOR MATCHES "(x86)|(X86)|(amd64)|(AMD64)")
        link_directories("/opt/homebrew/lib")
        include_directories("/opt/homebrew/include")
    endif()
endfunction(compile_bench)

foreach(ds ${Targets})
    compile_bench(${ds})
endforeach()

if (ALL_RENCODER AND CMAKE_SYSTEM_PROCESSOR MATCHES "(x86)|(X86)|(amd64)|(AMD64)")
    add_executable(bench_rencoder_se filters_benchmark/bench_rencoder.cpp)
    target_compile_definitions(bench_rencoder_se PRIVATE SET_VERSION="REncoderSE")
    target_link_libraries(bench_rencoder_se argparse)
    add_executable(bench_rencoder_ss filters_benchmark/bench_rencoder.cpp)
    target_compile_definitions(bench_rencoder_ss PRIVATE SET_VERSION="REncoderSS")
    target_link_libraries(bench_rencoder_ss argparse)
endif()

add_executable(workload_gen workload_gen.cpp)
target_link_libraries(workload_gen argparse)
